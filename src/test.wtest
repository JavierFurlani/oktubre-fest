import Personas.*
import Marcas.*
import Carpas.*


describe "Personas y Jarras" {
	const hofbrau = new Rubia(paisFabricacion = "Alemania", contenidoLupuloPorLitro = 10, graduacion = 0.08)
	const jarraHofbrau = new Jarras(marca = hofbrau, capacidad = 0.5)
	const jarraHofbrauGrande = new Jarras(marca = hofbrau, capacidad = 1.5)
	
	const javier = new PersonaBelga(peso = 100, leGustaMusicaTradicional = true, nivelDeAguante = 100)
	const fernando = new PersonaAlemana(peso = 140, leGustaMusicaTradicional = true, nivelDeAguante = 10)
	const nacho = new PersonaAlemana(peso = 200, leGustaMusicaTradicional = true, nivelDeAguante = 10)
	
	const carpaCervezaRubia = new Carpa(
		limiteDeGenteAdmitida = 10, 
		personasEnCarpa = [javier, fernando, nacho], 
		tocaBandaTradicional = true, 
		marcaDeCerveza = hofbrau
		) 
	
	method initialize() {
		javier.agregarJarra(jarraHofbrau)
		javier.agregarJarra(jarraHofbrau)
		javier.agregarJarra(jarraHofbrau)	
		5.times{i => fernando.agregarJarra(jarraHofbrauGrande)}
		5.times{i => nacho.agregarJarra(jarraHofbrauGrande)}
	}
		
	test "El contenido de alcohol de una jarra de cerveza de 8% de graduacion es de 0.04" {
		assert.equals(0.04, jarraHofbrau.contenidoDeAlcohol())
	}
	test "El total de alcohol que ingirio Javier es 0.12" {
		assert.equals(0.12, javier.totalDeAlcohol())
	}
	test "Javier no esta ebrio luego de tomar las 3 cervezas" {
		assert.notThat(javier.estaEbria())
	}
	test "A javier le gusta la cerveza Rubia" {
		assert.that(javier.leGusta(hofbrau))
	}
	test "La carpa le da cerveza a Javier" {
		carpaCervezaRubia.servirCerveza(javier, 2.5)
		assert.equals(4, javier.jarrasCompradas().size())		
	}
	test "personas ebrias en la carpa" {
		assert.equals(2, carpaCervezaRubia.cantEbriosEmpedernidos())
	}	
}
	